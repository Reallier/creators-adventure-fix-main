plugins {
    id 'com.github.johnrengelman.shadow'
    id "com.modrinth.minotaur" version "2.+"
//    id "com.matthewprenger.cursegradle" version "1.4.0"
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath
}


architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:$rootProject.fabric_loader_version"

    modImplementation "net.fabricmc.fabric-api:fabric-api:$rootProject.fabric_api_version"

    modImplementation "dev.architectury:architectury-fabric:$rootProject.architectury_api_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionFabric')

//    modImplementation "curse.maven:tiny-mob-farm-remastered-986607:${project.tiny_mob_farm_fabric_curseforge_id}-sources-${project.tiny_mob_farm_fabric_source_curseforge_id}"
    modApi "curse.maven:tiny-mob-farm-remastered-986607:${project.tiny_mob_farm_fabric_curseforge_id}"

    // 此处放入 FTB Ultimine 依赖项
    modImplementation("curse.maven:ftb-ultimine-fabric-448231:${project.ftbultimine_fabric_curseforge_id}")
    modRuntimeOnly("curse.maven:ftb-library-fabric-438495:${project.ftblibrary_fabric_curseforge_id}")

}

processResources {
    inputs.property 'version', project.version

    filesMatching('fabric.mod.json') {
        expand version: project.version
    }
}

shadowJar {
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}
